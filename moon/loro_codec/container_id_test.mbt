test "container_id to_bytes/from_bytes root" {
  let cid = ContainerID::root("root", ContainerType::map())
  let bytes = cid.to_bytes()
  assert_eq(bytes, b"\x80\x04root")
  let decoded = try! container_id_from_bytes(bytes[:])
  match decoded {
    ContainerID::Root(name, kind) => {
      assert_eq(name, "root")
      assert_eq(kind, ContainerType::map())
    }
    _ => assert_true(false)
  }
}

test "container_id to_bytes/from_bytes normal" {
  let cid = ContainerID::normal(7, 42, ContainerType::list())
  let bytes = cid.to_bytes()
  assert_eq(bytes, b"\x01\x07\x00\x00\x00\x00\x00\x00\x00\x2A\x00\x00\x00")
  let decoded = try! container_id_from_bytes(bytes[:])
  match decoded {
    ContainerID::Normal(peer, counter, kind) => {
      assert_eq(peer, 7)
      assert_eq(counter, 42)
      assert_eq(kind, ContainerType::list())
    }
    _ => assert_true(false)
  }
}

test "postcard option<container_id> matches rust" {
  // These bytes are generated by Rust `postcard::to_allocvec` on Option<ContainerID>.
  assert_eq(postcard_encode_option_container_id(None), b"\x00")

  let root_map = ContainerID::root("root", ContainerType::map())
  assert_eq(
    postcard_encode_option_container_id(Some(root_map)),
    b"\x01\x00\x04root\x01",
  )

  let root_text = ContainerID::root("t", ContainerType::text())
  assert_eq(
    postcard_encode_option_container_id(Some(root_text)),
    b"\x01\x00\x01t\x00",
  )

  let normal_list = ContainerID::normal(7, 42, ContainerType::list())
  assert_eq(
    postcard_encode_option_container_id(Some(normal_list)),
    b"\x01\x01\x07\x54\x02",
  )

  let (decoded, rest) = try! postcard_decode_option_container_id(b"\x01\x01\x07\x54\x02"[:])
  assert_eq(rest.length(), 0)
  match decoded {
    Some(ContainerID::Normal(peer, counter, kind)) => {
      assert_eq(peer, 7)
      assert_eq(counter, 42)
      assert_eq(kind, ContainerType::list())
    }
    _ => assert_true(false)
  }
}
